apply plugin: rootProject.ext.plugins.application
apply plugin: rootProject.ext.plugins.retrolambda

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        applicationId "com.hsj.app.host"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        // 矢量图
        vectorDrawables.useSupportLibrary = true

        //打包时筛选出这两种架构
        ndk { abiFilters "armeabi-v7a", "x86" }

        testInstrumentationRunner rootProject.ext.dependencies["testInstrumentationRunner"]
    }

    signingConfigs {
        config {
            keyAlias 'FastAndroid'
            keyPassword 'shengjunhu'
            storeFile file('App.jks')
            storePassword 'shengjunhu'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "App_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_v${defaultConfig.versionCode}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    productFlavors {
        baidu {
            applicationId 'com.hsj.app1'
        }
        tencent {
            applicationId 'com.hsj.app2'
        }
        xiaomi {
            applicationId 'com.hsj.app3'
        }
        qihu {
            applicationId 'com.hsj.app4'
        }
        huawei {
            applicationId 'com.hsj.app5'
        }
    }

    packagingOptions {
        exclude 'AndroidManifest.xml'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile rootProject.ext.dependencies["testCompile"]
    compile project(path: ':Library:BaseLib')
    compile rootProject.ext.dependencies["repluginHost"]
}

apply plugin: rootProject.ext.plugins.repluginHost

/**
 * 配置项均为可选配置，默认无需添加
 * 更多可选配置项参见replugin-host-gradle的RepluginConfig类
 * 可更改配置项参见 自动生成RePluginHostConfig.java
 */
repluginHostConfig {
    /**
     * 是否使用 AppCompat 库
     * 不需要个性化配置时，无需添加
     */
    useAppCompat = true
    /**
     * 背景不透明的坑的数量
     * 不需要个性化配置时，无需添加
     */
    countNotTranslucentStandard         = 6
    countNotTranslucentSingleTop        = 2
    countNotTranslucentSingleTask       = 3
    countNotTranslucentSingleInstance   = 2
}
